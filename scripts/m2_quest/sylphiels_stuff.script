//special dynamic quest items

#scope server

{
	setcallback touch enable
}

{ game_spawn
	setmodel misc/sylphiels_stuff.mdl
	width 16
	height 16
	setsolid trigger
	invincible 1
	nopush 1
	setvard PLAYING_DEAD 1 //not a valid target
	setvard IS_ACTIVE 1
	callevent 1.0 glow_loop
}

{ glow_loop
	if IS_ACTIVE
	callevent 10.0 glow_loop
	effect glow ent_me (255,200,0) 16.0 9.0 -1
}

{ game_touch
	if game.time > NEXT_TOUCH
	setvard NEXT_TOUCH game.time
	add NEXT_TOUCH 1.0

	if $get(PARAM1,isplayer)
	setvard QUEST_PLAYER PARAM1			
	callevent found_item
}

{ found_item
	setvard IS_ACTIVE 0
	playsound 0 10 items/ammopickup1.wav
	local OUT_MSG "You find "
	stradd OUT_MSG $get(ent_me,name.full)
	gplayermessage QUEST_PLAYER "You acquire " $get(ent_me,name.full)
	infomsg QUEST_PLAYER "QUEST ITEM FOUND" OUT_MSG
	helptip QUEST_PLAYER questitem "QUEST ITEM" "This is a special quest item that will not appear in your inventory."
	callexternal QUEST_PLAYER ext_got_quest_item ITEM_TYPE
	callevent 0.5 remove_me
}

{ remove_me
	deleteent ent_me
}

{ game_postspawn //<unused> <unused> <unused> <type>

	setvard ITEM_TYPE PARAM4
	if ( ITEM_TYPE equals ap )
	{
		name "a|Golden Apple"
		setmodelbody 0 0
		local L_DID_INIT 1
	}

	if ( ITEM_TYPE equals bs )
	{
		name "a|Bag of Salt"
		setmodelbody 0 1
		local L_DID_INIT 1
	}

	if ( ITEM_TYPE equals bp )
	{
		name "a|Bag of Pepper"
		setmodelbody 0 2
		local L_DID_INIT 1
	}

	if ( ITEM_TYPE equals km )
	{
		name "a|Barrel of Mead"
		setmodelbody 0 3
		local L_DID_INIT 1
	}

	if ( ITEM_TYPE equals la )
	{
		name "Sylphiel's Ladel"
		setmodelbody 0 4
		local L_DID_INIT 1
	}

	if !L_DID_INIT
	local OUT_MSG "m2_quest/sylphiels_stuff cannot find type: "
	stradd OUT_MSG ITEM_TYPE
	infomsg all "MAPPING ERROR" OUT_MSG
}

{ [server] bd_debug  //<print_var> <property_printvar> <extra> <extra> <caller_id>
	//tdebug/tdebugt
	saytextrange 2048
	saytext var PARAM1 prop PARAM2 = $get(PARAM1,PARAM2)
	consolemsg PARAM5 $get(ent_me,name) reports: var PARAM1 pvar $get(PARAM1,PARAM2)
	dbg PARAM5 $get(ent_me,name) reports: var PARAM1 pvar $get(PARAM1,PARAM2)
}